#!/usr/bin/env zsh

autoload colors && colors

# Create a new symbolic link, but remove it first if it already exists.
create_link () {
  if [ -e $2 ] || [ -L $2 ]; then
    rm $2
  fi

  ln -s $1 $2

  echo "$fg[green]  ☆  $2 linked.$reset_color"
}

# Prompt the user to take action if a symbolic link already exists.
# Y: Remove the link and replace it
# n: Skip this file
# A: Remove and re-link all following symbolic links
# q: Quit and exit with exit code 1
link_exists_prompt () {
  echo "$fg[yellow]  ☆  $2 already exists. Overwrite? [(Y)es (n)o (A)ll (N)o all (q)uit]$reset_color"

  read -ks action

  case $action in
    Y)
      create_link $1 $2
      ;;
    A)
      link_all=1
      create_link $1 $2
      ;;
    N)
      link_none=1
      echo "$fg[red]  ☆  Not linking $1.$reset_color"
      ;;
    q)
      echo "$fg[red]Goodbye.$reset_color"
      exit 1
      ;;
    *)
      echo "$fg[red]  ☆  Not linking.$reset_color"
  esac
}

echo "\n$fg[green]Installing symlinks.$reset_color"

for symlink (./**/*.symlink) {
  if [[ "$symlink" =~ "vim.symlink/files/undo" ]]; then
    continue
  fi

  link_to_path="$(pwd)$(echo $symlink | sed 's/\.//')"
  link_from_path="$HOME/.$(basename $symlink | awk -F. '{ $NF=""; print }' | sed 's/\ \([^$]\)/\.\1/' | sed 's/\ $//')"

  if [ $link_all ]; then
    create_link $link_to_path $link_from_path
  else
    if [ -e $link_from_path ] || [ -L $link_from_path ]; then
      if [ $link_none ]; then
        echo "$fg[red]  ☆  Not linking $link_to_path.$reset_color"
      else
        link_exists_prompt $link_to_path $link_from_path
      fi
    else
      create_link $link_to_path $link_from_path
    fi
  fi
}

if [[ "$(command -v brew)" == "" ]]; then
  echo "\n$fg[green]Installing Homebrew$reset_color"
  /usr/bin/ruby -e \
    "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)"
fi

echo "\n$fg[green]Installing Homebrew recipes$reset_color"

homebrew_install() {
  if [[ "$(brew ls --versions $1)" == "" ]]; then
    echo "$fg[green]  ☆  Installing $1.$reset_color"
    brew install $@
  else
    echo "$fg[yellow]  ☆  $1 already installed.$reset_color"
  fi
}

while read recipe; do
  if [ $install_none ]; then
    echo "$fg[red]  ☆  Not installing $recipe.$reset_color"
    continue
  fi

  if [ $install_all ]; then
    homebrew_install $(echo $recipe)
    continue
  fi

  echo "\n  Install $fg[green]$recipe$reset_color? [(Y)es (n)o (A)ll (N)o all (q)uit]"

  read -ks action

  case $action in
    Y)
      homebrew_install $(echo $recipe)
      ;;
    A)
      install_all=1
      homebrew_install $(echo $recipe)
      ;;
    N)
      install_none=1
      echo "$fg[red]  ☆  Not installing $recipe.$reset_color"
      ;;
    q)
      echo "$fg[red]Goodbye.$reset_color"
      exit 1
      ;;
    *)
      echo "$fg[red]  ☆  Not installing.$reset_color"
  esac
done < ./homebrew-recipes.txt

echo "\n$fg[green]Installing z.sh$reset_color"
wget -O $HOME/.z.sh \
  https://raw.githubusercontent.com/rupa/z/master/z.sh

echo "\n$fg[green]Installing Vim plugins$reset_color"
vim +PlugInstall +qa

echo "\n$fg[green]Installing Tmux plugin manager$reset_color"
if ! [ -d ~/.tmux/plugins ]; then
  mkdir -p ~/.tmux/plugins
  git clone https://github.com/tmux-plugins/tpm ~/.tmux/plugins/tpm
fi

echo "\n$fg[green]Installing terminfos$reset_color"
for terminfo_file (./terminfos/*.terminfo); do
  tic $terminfo_file
done

exit 0
